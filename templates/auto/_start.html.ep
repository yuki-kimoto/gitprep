<%
  # API
  my $api = gitprep_api;
  
  # Operator
  my $op = param('op') || '';
  
  my $errors;
  if ($op eq 'create') {
    
    # Sleep to protect password atack
    sleep 3 unless $ENV{GITPREP_TEST};
    
    # Check existence admin user
    my $admin_user = app->manager->admin_user;
    if (defined $admin_user) { $errors = ['Admin user already exists.'] }
    else {
      # Validation
      my $params = $api->params;
      my $rule = [
        password => [
          ['not_blank' => 'Password is empty.'],
          ['ascii' => 'Password contains invalid character.'],
          [{'length' => {max => 20}} => 'Password is too long.']
        ],
        {password_check => [qw/password password2/]}
          => {copy => 0}
          => [
            ['duplication' => "Two password don't match."]
          ]
      ];
      my $vresult = $self->app->validator->validate($params, $rule);
      
      if ($vresult->is_ok) {
        
        # Valida parameters
        my $params = $vresult->data;
        my $user = 'admin';
        $params->{admin} = 1;
        my ($password_encryped, $salt)
          = $api->encrypt_password($params->{password});
        $params->{password} = $password_encryped;
        $params->{salt} = $salt;
        
        # Create admin user
        $self->app->dbi->model('user')->insert($params, id => $user);
        
        # Redirect
        $self->flash(admin_user_created => 1);
        $self->redirect_to('/_login');
        $self->finish_rendering;
        return;
      }
      else { $errors = $vresult->messages }
    }
  }
%>

% layout 'common';

  %= include '/include/header';

  <div class="container">
    % if ($errors) {
      <div class="alert alert-error">
        <button type="button" class="close" data-dismiss="alert">&times;</button>
        % for my $error (@$errors) {
          <div><%= $error %></div>
        % }
      </div>
    % }
    <div class="text-center"><h3>Create Admin User</h3></div>
    <div class="well" style="background:white;padding-top:15px;padding-left:60px;width:300px;margin-left:auto;margin-right:auto">
      <form action="<%= url_for->query(op => 'create') %>" method="post">
        <div class="control-group">
          <label class="control-label" for="user-name">User name</label>
          <div>
            <b>admin</b>
          </div>
        </div>
        <div class="control-group">
          <label class="control-label" for="input-password">Password</label>
          <div class="controls">
            <%= password_field 'password', id => 'input-password', placeholder => 'Password' %>
            <%= password_field 'password2', id => 'input-password', placeholder => 'Password Again' %>
          </div>
        </div>
        <div class="control-group">
          <div class="controls">
            <button type="submit" class="btn">Create Admin User</button>
          </div>
        </div>
      </form>
    </div>
  </div>

  %= include '/include/footer';
