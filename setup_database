#!/usr/bin/env perl

use FindBin;
use lib "$FindBin::Bin/lib";
use lib "$FindBin::Bin/extlib/lib/perl5";
use DBIx::Custom;
use File::Path qw(make_path);

# make the data folder here
my $dir = 'data/rep';
if (!-e $dir) {
    make_path $dir or die "Error creating directory: $dir";
}

$dir = 'data/work';
if (!-e $dir) {
    make_path $dir or die "Error creating directory: $dir";
}

# setup the database

my $database_file = shift // "$FindBin::Bin/data/gitprep.db";

#  DBI
my %dbi_args = (
  dsn => "dbi:SQLite:database=$database_file",
  connector => 1,
  option => {sqlite_unicode => 1, sqlite_use_immediate_transaction => 1}
);
my $dbi = DBIx::Custom->connect(%dbi_args);

# Database state
my $database_state;
if (!-f $database_file) {
  $database_state = 'empty';
}
else {
  # If project.user_id exists, that database is version 1
  eval { $dbi->select('user_id', table => 'project', append => 'limit 0, 1') };
  
  if ($@) {
    $database_state = 'current';
  }
  else {
    $database_state = 'v1';
  }
}

# Need upgrade
if ($database_state eq 'v1') {
  die "Can't setup database. you maybe need upgrade database";
}


# Table definitions.
my @tables = (
  {
    name => 'user',
    fields => [
      {name => 'id', unique => 1, default => q{''}},
      {name => 'email', unique => 1, default => q{''}},
      {name => 'admin', type => 'integer', default => '0'},
      {name => 'password', default => q{''}},
      {name => 'salt', default => q{''}},
      {name => 'name', default => q{''}}
    ]
  },
  {
    name => 'project',
    fields => [
      {name => 'user', type => 'integer', default => '0'},
      {name => 'id'},
      {name => 'original_project', type => 'integer', default => '0'},
      {name => 'private', type => 'integer', default => '0'},
      {name => 'ignore_space_change', type => 'integer', default => '0'},
      {name => 'guess_encoding', default => q{''}},
      {name => 'website_url', default => q{''}}
    ],
    unique => ['user', 'id']
  },
  {
    name => 'ssh_public_key',
    fields => [
      {name => 'key', unique => 1, default => q{''}},
      {name => 'user', type => 'integer', default => '0'},
      {name => 'title', default => q{''}}
    ]
  },
  {
    name => 'collaboration',
    fields => [
      {name => 'project', type => 'integer', default => '0'},
      {name => 'user', type => 'integer', default => '0'}
    ],
    unique => ['project', 'user']
  },
  {
    name => 'issue',
    fields => [
      {name => 'project', type => 'integer', default => '0'},
      {name => 'number', type => 'integer', default => '0'},
      {name => 'title', default => q{''}},
      {name => 'open', type => 'integer', default => '0'},
      {name => 'open_time', type => 'integer', default => '0'},
      {name => 'open_user', type => 'integer', default => '0'},
      {name => 'pull_request', type => 'integer', default => '0'}
    ],
    unique => ['project', 'number']
  },
  {
    name => 'issue_message',
    fields => [
      {name => 'issue', type => 'integer', default => '0'},
      {name => 'number', type => 'integer', default => '0'},
      {name => 'message', default => q{''}},
      {name => 'create_time', type => 'integer', default => '0'},
      {name => 'update_time', type => 'integer', default => '0'},
      {name => 'user', type => 'integer', default => '0'}
    ],
    unique => ['issue', 'number']
  },
  {
    name => 'pull_request',
    fields => [
      {name => 'base_project', type => 'integer', default => '0'},
      {name => 'base_branch', default => q{''}},
      {name => 'target_project', type => 'integer', default => '0'},
      {name => 'target_branch', default => q{''}},
      {name => 'create_time', type => 'integer', default => '0'},
      {name => 'update_time', type => 'integer', default => '0'},
      {name => 'user', type => 'integer', default => '0'}
    ],
    unique => ['base_project', 'base_branch', 'target_project', 'target_branch']
  },
  {
    name => 'label',
    fields => [
      {name => 'project', type => 'integer', default => '0'},
      {name => 'id', type => 'varchar(100)', default => q{''}},
      {name => 'description', default => q{''}},
      {name => 'color', default => q{''}}
    ],
    unique => ['project', 'id']
  },
  {
    name => 'issue_label',
    fields => [
      {name => 'issue', type => 'integer', default => '0'},
      {name => 'label', type => 'integer', default => '0'}
    ],
    unique => ['issue', 'label']
  },
  {
    name => 'wiki',
    fields => [
      {name => 'project', type => 'integer', default => '0'}
    ],
    unique => ['project']
  },
  {
    name => 'subscription',
    fields => [
      {name => 'issue', type => 'integer', default => '0'},
      {name => 'user', type => 'integer', default => '0'},
      {name => 'reason', type => 'text', default => q{''}}
    ],
    unique => ['issue', 'user']
  },
  {
    name => 'watch',
    fields => [
      {name => 'user', type => 'integer', default => '0'},
      {name => 'project', type => 'integer', default => '0'}
    ],
    unique => ['user', 'project']
  },
);


sub create_table {
  my ($table) = @_;

  # Create table if needed and make sure all fields are present.
  my $create = "create table $table->{name} (row_id integer primary key autoincrement";
  my @alters;
  my @columns = ('row_id');
  foreach my $field (@{$table->{fields}}) {
    my $define = $field->{name};
    $define .= " $field->{type}" if $field->{type};
    $define .= ' not null' unless $field->{nullable};
    $define .= ' unique' if $field->{unique};
    $define .= " default $field->{default}" if defined $field->{default};
    $create .= ", $define";
    push @alters, $define;
    push @columns, $field->{name};
  };
  $create .= ', unique(' . join(', ', @{$table->{unique}}) . ')' if $table->{unique};

  # Create table if not yet done.
  eval { $dbi->execute("$create);") };

  # Add fields that may not be present.
  foreach my $alter (@alters) {
    eval { $dbi->execute("alter table $table->{name} add column $alter") };
  }

  # Check that everything is ok.
  eval { $dbi->select(\@columns, table => $table->{name}) };
  if ($@) {
    my $error = "Can't create $table->{name} table properly: $@";
    die $error;
  }
}


# Create database

foreach my $table (@tables) {
  create_table($table);
}
