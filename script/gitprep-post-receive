#!/usr/bin/env perl

use strict;
use warnings;
use utf8;
use FindBin;
use lib "$FindBin::Bin/../lib";
use lib "$FindBin::Bin/../extlib/lib/perl5";
use Mojo::URL;
use Gitprep::RPC;

my $channel_fileno = 99;  # File descriptor number for RPC channel.

# Retrieve our parameters
my ($user_id, $project_id) = @ENV{'GITPREP_USER', 'GITPREP_PROJECT'};

die "Gitprep parameters unset" unless $user_id && $project_id;

# Establish the RPC connection.
open my $channel, "+<&=$channel_fileno" or die "Can't open request channel\n";
my $rpc = Gitprep::RPC->new(sub {syswrite $channel, shift});

sub rpc_call {
  $rpc->request(@_);
  my ($input, $result, $error, $gotit);
  while (!$gotit) {
    sysread $channel, $input, 2048 or die "Gitprep shell has exited\n";
    $rpc->feed($input);
    ($result, $error, $gotit) = $rpc->result;
  }
  die $error if $error;
  return $result;
}

my $rep_info = rpc_call('rep_info($user_id, $project_id)',
  user_id => $user_id,
  project_id => $project_id
);
my $default_branch = rpc_call('git->current_branch($rep_info)',
  rep_info => $rep_info
);
my $project = rpc_call('dbi->model("project")->select(where => $where)->one',
  where => {'user.id' => $user_id, 'project.id' => $project_id}
);

my $new_pr_path;
my $url = rpc_call('config->{basic}{new_pr_url}');
if ($url) {
  $url = Mojo::URL->new($url);
  if ($url && $url->host) {
    $url->scheme('http') unless $url->scheme;
    $url->userinfo(undef);
    $url->query(undef);
    $url->fragment(undef);
    $new_pr_path = $url->path;
    $new_pr_path =~ s#/*$##;
    $new_pr_path .= "/$user_id/$project_id/pull/new";
  }
}

while (<>) {
  chomp;
  die "Invalid update data" unless $_ =~
    m#^([0-9a-f]{40})\s([0-9a-f]{40})\s(refs/(tags|heads|remotes)/(\S+))$#i;
  my ($old, $new, $ref, $kind, $name) = ($1, $2, $3, $4, $5);
  $old =~ s/0{40}//;
  $new =~ s/0{40}//;

  if ($new_pr_path && $kind eq 'heads' && !$old) {
    if ($project->{original_project} || $name ne $default_branch) {
      $url->path("$new_pr_path/$name");
      print "\nCreate a pull request for '$name' on Gitprep by visiting:\n";
      print "     $url\n\n";
    }
  }
}

exit 0;
